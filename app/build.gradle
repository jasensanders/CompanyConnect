apply plugin: 'com.android.application'

android {

    // Reference Keys and add to signing config.
    def alias = PlayStoreAlias
    def password = KeyPassowrd
    def storeFilePath = storeFilePath
    def StorePassword = StorePassword

    // Signing config
    signingConfigs {

        ReleaseConfig {

            keyAlias = alias
            keyPassword = password
            storeFile = file(storeFilePath)
            storePassword = StorePassword
        }
    }

    compileSdkVersion 27

    defaultConfig {

        applicationId "com.enrandomlabs.jasensanders.v1.superiorconnect"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.ReleaseConfig
            buildConfigField "boolean", "DEBUG_BUILD", "false"

        }

        debug {

            jniDebuggable true
            buildConfigField "boolean", "DEBUG_BUILD", "true"

        }
    }

    buildTypes.each {

        // API keys references
        it.buildConfigField 'String', 'SEND_MAIL_SERVER', SuperiorMailServer
        it.buildConfigField 'String', 'SEND_MAIL_USERNAME', SuperiorMailUsername
        it.buildConfigField 'String', 'SEND_MAIL_PW', SuperiorMailPW
        it.buildConfigField 'String', 'SEND_NOTE', SuperiorMailNote
        it.buildConfigField 'String', 'SEND_ESTIMATE', SuperiorMailEstimate
        it.buildConfigField 'String', 'SEND_EWASTE', SuperiorMailE_waste

    }

    // This resolves a file issue while using the android sun.mail api
    packagingOptions{

        pickFirst 'META-INF/mimetypes.default'
        pickFirst 'META-INF/mailcap.default'

    }
}

dependencies {

    // Standard App Dependencies
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.sun.mail:android-mail:1.6.0'
    implementation 'com.sun.mail:android-activation:1.6.0'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    testImplementation 'junit:junit:4.12'

    // Android Testing Support Libraries
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // UI Testing Dependencies
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'

}